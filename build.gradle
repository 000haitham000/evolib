apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group 'com.github.000haitham000'
version '2.1.0'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

repositories {
    mavenCentral()
}

def uploadToOSSRH = false;

if (uploadToOSSRH) {
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
                pom.project {
                    name 'evolib'
                    packaging 'jar'
                    // optionally artifactId can be defined here
                    description 'An open source Java implementation of NSGA-II, NSGA-III and Unified NSGA-III (U-NSGA-III)'
                    url 'https://github.com/000haitham000/evolib'

                    scm {
                        connection 'scm:git:git://github.com/000haitham000/evolib.git'
                        developerConnection 'scm:git:ssh://github.com/000haitham000/evolib.git'
                        url 'https://github.com/000haitham000/evolib'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'hseada'
                            name 'Haitham Seada'
                            email '000haitham000@gmail.com'
                        }
                    }
                }
            }
        }
    }
}

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

dependencies {
    // JUnit
    testCompile group: 'junit', name: 'junit', version: '4.10'
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
    // https://mvnrepository.com/artifact/commons-io/commons-io
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    // TX2EX
    implementation 'com.github.000haitham000:tx2ex:2.0.1'
    // KKTPM
    implementation 'com.github.000haitham000:kktpm-calculator:2.1.0'
}
